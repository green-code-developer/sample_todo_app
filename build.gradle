plugins {
	id 'java'
	id 'org.springframework.boot' version '3.4.4'
	id 'io.spring.dependency-management' version '1.1.7'
	id 'war'
	id 'org.jooq.jooq-codegen-gradle' version '3.19.21'
}

group = 'jp.green-code'
version = '0.0.1-SNAPSHOT'

java {
	sourceCompatibility = '21'
}

repositories {
	mavenCentral()
}

dependencies {
	// Spring web
	implementation 'org.springframework.boot:spring-boot-starter-web'
	// HTML テンプレートエンジン thymeleaf
	implementation 'org.springframework.boot:spring-boot-starter-thymeleaf'
	// validation
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	// セッションをDB 保存
	implementation 'org.springframework.session:spring-session-core'
	// DB 接続 jooq
	implementation 'org.springframework.boot:spring-boot-starter-jooq'
	runtimeOnly 'org.postgresql:postgresql'
	jooqCodegen 'org.postgresql:postgresql'
	// セキュリティ
	implementation 'org.springframework.boot:spring-boot-starter-security'
	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	// 主にStringUtils を利用
	implementation 'org.apache.commons:commons-lang3:3.14.0'
	// ファイル操作で利用
	implementation 'commons-io:commons-io:2.15.1'
	// Actuator 生死監視用
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	// テスト
	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// webjar でバージョンを指定しないで使う
	implementation 'org.webjars:webjars-locator:0.52'
	// webjar ここから
	implementation 'org.webjars:font-awesome:6.5.2'
	// webjar ここまで
}

tasks.named('test') {
	useJUnitPlatform()
}

jooq {
	configuration {
		jdbc {
			driver = "org.postgresql.Driver"
			url = "jdbc:postgresql://localhost:56432/todo"
			user = "todo"
			password = "todo"
		}
		generator {
			database {
				name = "org.jooq.meta.postgres.PostgresDatabase"
				inputSchema = "todo"
				includes = ".*"
				excludes = "spring_session.*"
				forcedTypes{
					forcedType{
						name = "ENUM"
						userType = "jp.green_code.todo.enums.TodoStatusEnum"
						includeTypes = "todo_status"
					}
				}
			}
			target {
				packageName = "jp.green_code.todo.jooq"
			}
			generate {
				pojos = true
			}
		}
	}
}
sourceSets {
	main {
		java {
			srcDir 'build/generated-sources/jooq'
		}
	}
}